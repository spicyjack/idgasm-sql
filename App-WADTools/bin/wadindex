#!/usr/bin/perl -w

# For support with this file, please file an issue on the GitHub issue tracker
# for this project: https://github.com/spicyjack/wadtools/issues

=head1 NAME

B<wadindex> - Create an index and/or catalog of WAD files using files stored
on a local machine

=cut

our $copyright = qq|Copyright (c) 2013 by Brian Manning|;

=head1 SYNOPSIS

 perl wadindex [OPTIONS]

 Script options:
 -h|--help          Shows this help text
 -d|--debug         Debug script execution (tons of output)
 -c|--colorize      Always colorize script output
 -v|--verbose       Verbose script execution
 --version          Print script version and copyright, then exit

 Path/database options:
 -p|--path          Path to WAD files/directories with WAD files
 --strip-path       Strip this string out of log output
 -t|--tempdir       Temporary directory to use when unzipping WAD files
 -i|--idgames-db    Database created with 'idg_db_dump' schema
 -x|--index-db      Database created with 'wadindex' schema

 Debug options:
 --debug-noexit     Don't exit script when --debug is used
 --debug-requests   Exit after this many files when --debug used

 Example usage:

 # walk a directory structure full of '*.zip' files and index
 wadindex --path /path/to/wad/files --tempdir /dev/shm

 # run wadindex with debugging output, process 100 zip/non-zip files,
 # then exit
 wadindex --path /path/to/wad/files --tempdir /dev/shm \
  --debug --debug-requests 100

 # Strip a string out of the --path argument;
 # good for traversing a local mirror of idGames Archive
 wadindex --path /real/long/path/to/idgames/archive \
    --strip-path "/real/long/path/to"


You can view the full C<POD> documentation of this file by calling C<perldoc
wadindex>.

=cut

our @options = (
    # script options
    q(debug|d),
    q(verbose|v),
    q(version),
    q(help|h),
    q(colorize|c), # ignore '-t' test and always colorize output

    # path/db options
    q(path|p=s),
    q(strip-path=s),
    q(tempdir|t=s),
    q(idgames-db|idgdb|i=s),
    q(index-db|idxdb|x=s),

    # debug options
    q(debug-noexit),
    q(debug-requests=i),
);

=head1 DESCRIPTION

Given a directory with WAD files (or WAD files zipped up
inside of '.zip' files), create an index that contains:

=over

=item The name of the WAD file

=item What levels that WAD file contains

=item The WAD's author

=item The WAD's checksum

=item The WAD's average rating on Doomworld

=back

=cut

################
# package main #
################
package main;
use 5.010;
use strict;
use warnings;
use utf8;

### System modules
use Carp;
use Data::Dumper;
$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Terse = 1;
use Fcntl;
use File::Basename;
use File::Find::Rule;
#use File::LibMagic;
use File::Temp;
use IO::File;
use Log::Log4perl qw(get_logger :no_extra_logdie_message);
use Log::Log4perl::Level;
use Pod::Usage;

### Local modules
use App::WADTools::Config;
use App::WADTools::idGamesDB;
use App::WADTools::WADIndexer;
use App::WADTools::ZipFile;

# FIXME: GitHub issue #83, create a MIME magic preflight check
# WARNING: these will change depending on which 'magic' file you're looking at
use constant {
    OCTET_STREAM => q(application/octet-stream;),
    TEXT_PLAIN   => q(text/plain; charset=),
    ZIP        => q(application/zip; charset=binary),
};

    # create a variable with the current name of the script
    my $my_name = basename($0);
    # provide a version string if dzil hasn't populated modules with $VERSION
    # scalars
    my $VERSION = $App::WADTools::VERSION || q(git-dev);

    # allow UTF-8 output
    binmode(STDOUT, ":utf8");

    # create a logger object
    my $cfg = App::WADTools::Config->new(options => \@options);

    # dump and bail if we get called with --help
    if ( $cfg->defined(q(help)) ) { pod2usage(-exitstatus => 1); }

    if ( $cfg->defined(q(version)) ) {
        print STDERR qq($my_name: version '$VERSION'\n);
        print STDERR qq($my_name: $copyright\n);
        exit 0;
    }

    if ( ! $cfg->defined(q(tempdir)) ) {
        # If no temporary directory is defined, set one.
        # - For Linux, use shared memory for temporary directory
        # - For other systems, leave 'tempdir' unset, which will cause
        # File::Temp to use the output of File::Spec->tmpdir
        if ( -d q(/dev/shm) ) {
            $cfg->set(q(tempdir), q(/dev/shm));
        }
    }
    # set up the logger
    my $log_conf;
    if ( $cfg->defined(q(debug)) ) {
        $log_conf = qq(log4perl.rootLogger = DEBUG, Screen\n);
    } elsif ( $cfg->defined(q(verbose)) ) {
        $log_conf = qq(log4perl.rootLogger = INFO, Screen\n);
    } else {
        $log_conf = qq(log4perl.rootLogger = WARN, Screen\n);
    }

    if ( -t STDOUT || $cfg->defined(q(colorize)) ) {
        $log_conf .= qq(log4perl.appender.Screen = )
            . qq(Log::Log4perl::Appender::ScreenColoredLevels\n);
    } else {
       $log_conf .= qq(log4perl.appender.Screen = )
            . qq(Log::Log4perl::Appender::Screen\n);
    }

    $log_conf .= qq(log4perl.appender.Screen.stderr = 1\n)
        . qq(log4perl.appender.Screen.utf8 = 1\n)
        . qq(log4perl.appender.Screen.layout = PatternLayout\n)
        . q(log4perl.appender.Screen.layout.ConversionPattern )
        # %r: number of milliseconds elapsed since program start
        # %p{1}: first letter of event priority
        # %4L: line number where log statement was used, four numbers wide
        # %M{1}: Name of the method name where logging request was issued
        # %m: message
        # %n: newline
        . qq|= [%6r] %p{1} %4L (%M{1}) %m%n\n|;
        #. qq( = %d %p %m%n\n)
        #. qq(= %d{HH.mm.ss} %p -> %m%n\n);

    # create a logger object, and prime the logfile for this session
    Log::Log4perl::init( \$log_conf );
    my $log = get_logger(""); # "" = root logger

    $log->logdie(qq(Missing '--path' directory argument))
        unless ( $cfg->defined(q(path)) );
    my $wad_path = $cfg->get(q(path));
    $log->logdie(qq('--path' )
        . $cfg->get(q(path)) . q( not found/available))
        unless ( -r $wad_path );

    # print a nice banner before checking for optional arguments
    $log->warn(qq(Starting $my_name, version '$VERSION'));
    $log->warn($copyright);
    $log->warn(qq(My PID is $$));
    $log->warn(qq(Searching for ZIP files in path:));
    $log->warn(qq(-> $wad_path));

    # check for the database files if either/both --idgames-db and/or
    # --index-db were used
    my ($idg_db, $idx_db);
    if ( $cfg->defined(q(idgames-db)) ) {
        $log->logdie(qq(Can't find idgames-db file ) . $cfg->get(q(idgames-db)))
            unless ( -e $cfg->get(q(idgames-db)) );
        $idg_db = App::WADTools::idGamesDB->new(
            filename => $cfg->get(q(idgames-db))
        );
        # with updated Database object, when check_schema => 1 is passed, the
        # return value is the number of rows in the schema table, aka how many
        # schema blocks that have been applied to the database
        my $idg_db_connect = $idg_db->connect( check_schema => 1 );
        if ( ref($idg_db_connect) eq q(App::WADTools::Error) ) {
            $log->error(q(Recieved error connecting to idgames-db));
            $log->error(q(Error type: ) . $idg_db->type);
            $log->logdie(q(Error message: ) . $idg_db->message);
        } else {
            $log->debug(qq(idgames-db has $idg_db_connect schema entries));
        }
    } else {
        $log->warn(q(WARNING: Missing optional parameter --idgames-db;));
        $log->warn(q(Script will continue, but will not be able to retrieve));
        $log->warn(q(info from idGames Archive for files being indexed));
    }

    if ( $cfg->defined(q(index-db)) ) {
        $log->logdie(qq(Can't find index-db file ) . $cfg->get(q(index-db)))
            unless ( -e $cfg->get(q(index-db)) );
        $idx_db = App::WADTools::idGamesDB->new(
            filename => $cfg->get(q(index-db))
        );
        # use check_schema to check schema; $idx_db_connect will be the number
        # of schema entries if there are no errors
        my $idx_db_connect = $idx_db->connect( check_schema => 1 );
        if ( ref($idx_db_connect) eq q(App::WADTools::Error) ) {
            $log->error(q(Recieved error connecting to idgames-db));
            $log->error(q(Error type: ) . $idx_db->type);
            $log->logdie(q(Error message: ) . $idx_db->message);
        } else {
            $log->debug(qq(index-db has $idx_db_connect schema entries));
        }
    } else {
        $log->warn(q(WARNING: Missing optional parameter '--index-db'));
        $log->warn(q(Script will continue, but will not record information));
        $log->warn(q(about files that are indexed by the script));
    }

    my $zip_file_counter = 0;
    my $unzip_error_counter = 0;
    my $total_file_counter = 0;
    my $indexer = App::WADTools::WADIndexer->new( idx_db => $idx_db );
    my @files = File::Find::Rule
                        ->file
                        #->name(q(*.wad), q(*.zip))
                        ->in($wad_path);
    FILE_LOOP: foreach my $found_file ( sort(@files) ) {
        $total_file_counter++;
        last FILE_LOOP if ( $cfg->defined(q(debug-requests))
            && $total_file_counter > $cfg->get(q(debug-requests)) );
        my $filename = basename($found_file);
        my $dirname = dirname($found_file);
        my $file_path = $dirname . q(/) . $filename;
        my $short_path;

        # create a "short path", to make the logs look nicer
        if ( $cfg->defined(q(strip-path)) ) {
            my $strip_path = $cfg->get(q(strip-path));
            #$log->debug(qq('strip-path' is $strip_path));
            $short_path = $dirname;
            $short_path =~ s/^$strip_path//;
            $short_path =~ s!^/!!;
            #$log->debug(qq(Setting short_path to $short_path));
            $file_path = qq($short_path/$filename);
        } else {
            $short_path = $dirname;
        }

        $log->warn(qq(=== File: $file_path ===));
        my $file;
        if ( defined $idg_db ) {
            $file = $idg_db->get_file_by_path(
                path     => $short_path . q(/),
                filename => $filename,
            );
            if ( defined $file ) {
                $log->info(qq(Path/filename matched file ID ) . $file->id
                    . q( in idgames-db));
            } else {
                $log->info(qq(No match in db: '$short_path/$filename'));
            }
        }
        if ( $filename =~ /\.zip$/ ) {
            my $zipfile = App::WADTools::ZipFile->new( file => $found_file );
            my @members = $zipfile->get_zip_members();
            my @wads_in_zip = grep(/\.wad/i, @members);
            if ( scalar(@wads_in_zip) > 0 ) {
                $log->info(q(Extracting ) . scalar(@wads_in_zip)
                    . qq( WADs from zipfile '$filename'));
                my $unzip_dir = $zipfile->extract_files(
                    files   => \@wads_in_zip,
                    tempdir => $cfg->get(q(tempdir)),
                );
                if ( defined $unzip_dir ) {
                    $indexer->index(
                        unzip_dir => $unzip_dir,
                        files     => \@wads_in_zip,
                    );
                } else {
                    $log->warn(qq(Could not unzip $filename));
                    $log->warn(qq(Skipping to next file));
                    $unzip_error_counter++;
                    next FILE_LOOP;
                }
            } else {
                $log->info(qq(No *.wad files in $file_path));
            }
            $zip_file_counter++;
        } else {
            $log->info(qq(Not a zip file: $filename));
        }
        $log->warn(qq(Done: $file_path));
    }

=head1 AUTHOR

Brian Manning, C<< <brian at xaoc dot org> >>

=head1 BUGS

Please report any bugs or feature requests to the GitHub issue tracker for
this project:

C<< <https://github.com/spicyjack/wadtools/issues> >>.

=head1 SUPPORT

You can find documentation for this script with the perldoc command.

    perldoc wadindex

=head1 COPYRIGHT & LICENSE

Copyright (c) 2013 Brian Manning, all rights reserved.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

=cut

# fin!
# vim: set shiftwidth=4 tabstop=4
